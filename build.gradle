buildscript {
	repositories {
		mavenCentral()
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://maven.blamejared.com' }
		maven {
			name = "Sponge"
			url = "https://repo.spongepowered.org/repository/maven-public"
		}
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
	}
}

plugins {
	id("java")
	id("java-library")
	id("maven-publish")
	id("eclipse")
	id("org.spongepowered.gradle.plugin") version "2.0.2"
}

apply plugin: 'net.minecraftforge.gradle'

group = "sawfowl.regionguard"
version = "${major}.${minor}.${patch}-${api}-${suffix}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava.options.compilerArgs << "-Xmaxerrs" << "9999"

minecraft {
	mappings channel: 'official', version: '1.19.4'
}

repositories {
	mavenCentral()

	maven {
		name = "Forge"
		url = 'https://maven.minecraftforge.net'
	}
	maven {
		name = "Sponge"
		url = "https://repo.spongepowered.org/repository/maven-public"
	}
	maven {
		name = "JitPack"
		url 'https://jitpack.io'
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
	implementation "org.spongepowered:spongeapi:10.0.0"
	implementation 'org.spongepowered:spongevanilla:1.19.4-10.0.0-RC1397:universal'
	implementation "com.github.SawFowl:LocaleAPI:${localeapi}"
	implementation "com.github.SawFowl:CommandPack:${commandpack}"
	minecraft 'net.minecraftforge:forge:1.19.4-45.2.0'
	//implementation fileTree(dir: 'libs', include: '*.jar')
}

sponge {
	apiVersion("${api}")
	loader {
		name("java_plain")
		version("1.0")
	}
	license("GPL-3.0")
	plugin("regionguard") {
		displayName(project.name.capitalize())
		version("${project.version}")
		entrypoint("${project.group}.${rootProject.name}")
		description("Create regions and protect them from various actions of other players.")
		links {
			homepage("https://discord.gg/7xnZGSYJH9")
			source("https://github.com/SawFowl/RegionGuard")
			issues("https://discord.gg/7xnZGSYJH9")
		}
		contributor("SawFowl") {
			description("Lead Developer")
		}
		dependency("spongeapi") {
			optional(false)
			loadOrder("after")
			version("8.0.0")
		}
		dependency("localeapi") {
			optional(false)
			loadOrder("after")
			version("${localeapi}")
		}
		dependency("commandpack") {
			optional(false)
			loadOrder("after")
			version("${commandpack}")
		}
		dependency("mysqldriver") {
			optional(true)
			loadOrder("after")
			version("1.0.0")
		}
		dependency("h2driver") {
			optional(true)
			loadOrder("after")
			version("1.0.0")
		}
		dependency("worldedit") {
			optional(true)
			loadOrder("after")
			version("7.2.0")
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

configurations {
	doc {
		transitive false
	}
}

javadoc {
	options.encoding = 'UTF-8'
	source configurations.doc.collect { zipTree(it) }
	options.addStringOption('Xdoclint:none', '-quiet')
}
